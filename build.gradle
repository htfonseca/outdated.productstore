buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
        asciidoctorVersion = '1.5.3'
        jUnitJupitorVersion = '5.3.1'
        snippetsDir = file('build/generated-snippets')
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

group = 'com.myprojects.spring'
version = '0.0.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    implementation('com.h2database:h2')


    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    testImplementation("org.junit.jupiter:junit-jupiter-api:${jUnitJupitorVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${jUnitJupitorVersion}")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${jUnitJupitorVersion}")
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

task copyRestDocs(type: Copy) {
    dependsOn asciidoctor
    from "${asciidoctor.outputDir}/html5"
    into "${sourceSets.main.output.resourcesDir}/static/docs"
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

bootRun {
    dependsOn copyRestDocs

}